apply plugin: "java"
apply plugin: "maven"
apply plugin: "signing"

group = "com.glookast.commons"
version = "3.0.0"

sourceCompatibility = 1.8
targetCompatibility = 1.8

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

jar {
    manifest {
        attributes "Implementation-Version": version
    }
}

repositories {
    mavenCentral()
    maven {
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok:1.18.6'
    compileOnly 'org.projectlombok:lombok:1.18.6'

    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.8'

    compile 'com.glookast.commons:commons-base:2.0.1'
    compile 'com.glookast.commons:commons-timecode:2.0.3'

    testCompile "junit:junit:4.12"
}

task javadocJar(type: Jar) {
    classifier = "javadoc"
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name "commons-capture"
                packaging "jar"
                // optionally artifactId can be defined here 
                description "Helper classes with Jackson annotations for easy interfacing with Glookast Products"
                url "http://github.com/glookast/commons-capture"

                scm {
                    connection "scm:git:git://github.com/glookast/commons-capture.git"
                    developerConnection "scm:git:ssh://github.com:glookast/commons-capture.git"
                    url "http://github.com/glookast/commons-capture/tree/master"
                }

                licenses {
                    license {
                        name "The Apache License, Version 2.0"
                        url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        id "joostrovers"
                        name "Joost Rovers"
                        email "joost.rovers@glookast.com"
                    }
                    developer {
                        id "tiagoafpereira"
                        name "Tiago Pereira"
                        email "tiago.pereira@glookast.com"
                    }
                }
            }
        }
    }
}